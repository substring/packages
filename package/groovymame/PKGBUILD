# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

pkgname=(groovymame
         groovymame-tools)
pkgver=0.281
pkgrel=1
pkgdesc="GroovyMAME is a M.A.M.E. fork aimed at CRT monitor"
_mamever=${pkgver/./}
_switchresver=221f
url="https://mamedev.org/"
license=(BSD-2-Clause
         BSD-3-Clause
         BSL-1.0
         CC0-1.0
         GPL-2.0-only
         LGPL-2.1-only
         MIT
         Zlib)
arch=(x86_64)
makedepends=(asio
             flac
             glm
             libpulse
             libutf8proc
	     libxi
             libxinerama
             libxrandr
             lua
             nasm
             portaudio
             portmidi
             pugixml
             python
             qt6-base
             rapidjson
             sdl2_ttf)
source=(git+https://github.com/antonioginer/GroovyMAME#tag=gm${_mamever}sr${_switchresver}
        mame.desktop
        mame.sh
	mame.svg)
sha256sums=(SKIP
            '0e5e93f84673c2d17a1bf7a6b256cfa54ef89a086a828df19bc054c12776d821'
            'd78e9561352cbcb814c94bad324a31efa1c6141c9e219132b0cdd3898e25f292'
	    '17c442c933d764175e4ce1de50a80c0c2ddd5d733caf09c3cd5e6ba697ac43f4')
options=(!debug
         !lto) # debug build fails

prepare() {
  cd GroovyMAME

# Use system libraries
  sed -e 's|\# USE_SYSTEM_LIB|USE_SYSTEM_LIB|g' -i makefile
# Use C++ LUA
  sed -e 's|ext_lib("lua")|ext_lib("lua++")|' -i scripts/src/main.lua -i scripts/src/3rdparty.lua
# Drop hardcoded libexec
  sed -e 's|libexec/||' -i scripts/src/osd/modules.lua
# Use bundled asio, 1.34 is too new
  export CXXFLAGS+=" -I$PWD/3rdparty/asio/include"
}

build() {
  cd GroovyMAME

  export CXXFLAGS+="  -Wp,-U_GLIBCXX_ASSERTIONS" # FS#73202

  make \
    NOWERROR=1 \
    OPTIMIZE=2 \
    TOOLS=1 TARGET=mame \
    QT_HOME=/usr/lib/qt6 \
    ARCHOPTS=-flifetime-dse=1 \
    USE_WAYLAND=1
}

package_groovymame() {
  depends=(expat
           flac
           fontconfig
           gcc-libs
           glibc
           libglvnd
           libjpeg-turbo
           libpulse
           libutf8proc
           libx11
           libxi
           libxrandr
           lua
           portaudio
           portmidi
           pugixml
           qt6-base
           sdl2
           sdl2_ttf
           sqlite
           zlib)
  conflicts=(sdlmame mame)
  replaces=(sdlmame mame)

  cd GroovyMAME

  # Install the mame script
  install -Dm755 "$srcdir"/mame.sh "$pkgdir"/usr/bin/mame

  # Install the binaries - This one is from the GroovyMAME repo
  #install -Dm755 "$srcdir"/groovymame "$pkgdir"/usr/lib/mame/${pkgname}
  # This is the binary that was compiled
  install -Dm755 mame "$pkgdir"/usr/lib/mame/${pkgname}

  # Install the extra bits
  install -Dm644 src/osd/modules/opengl/shader/glsl*.*h -t "$pkgdir"/usr/lib/mame/shader/
  cp -ar {artwork,bgfx,plugins,language,ctrlr,keymaps,hash} "$pkgdir"/usr/lib/mame/
  install -Dm 644 uismall.bdf "$pkgdir"/usr/lib/mame/fonts/uismall.bdf
  mkdir -p "$pkgdir"/usr/share/mame
  "$pkgdir"/usr/lib/mame/groovymame -listxml > "$pkgdir"/usr/share/mame/mame.dat

  # Include the license
  install -Dm644 docs/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # FS#28203
  sed -i 's|KEYCODE_2_PAD|KEYCODE_2PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_4_PAD|KEYCODE_4PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_6_PAD|KEYCODE_6PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_8_PAD|KEYCODE_8PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg

  # documentation
  install -dm0755 "$pkgdir"/usr/share/doc
  cp -a docs "$pkgdir"/usr/share/doc/$pkgname
  rm -r "$pkgdir"/usr/share/doc/$pkgname/man
  install -Dm644 docs/man/*.6* -t "$pkgdir"/usr/share/man/man6/

  # install desktop file and icon
  install -Dm644 "$srcdir"/mame.desktop -t "$pkgdir"/usr/share/applications
  install -Dm644 "$srcdir"/mame.svg -t "$pkgdir"/usr/share/icons/hicolor/scalable/apps
}

package_groovymame-tools() {
  pkgdesc='GroovyMAME is a M.A.M.E. fork aimed at CRT monitor (tools)'
  depends=(flac
           gcc-libs
           glibc
           libutf8proc
           sdl2
           zlib)


  cd GroovyMAME
  for _i in castool chdman floptool imgtool jedutil ldresample ldverify nltool nlwav pngcmp regrep romcmp \
            split srcclean testkeys unidasm; do
    install -Dm755 $_i -t "$pkgdir"/usr/bin
  done
  mv "$pkgdir"/usr/bin/{,mame-}split # Fix conflicts

  install -Dm644 docs/man/*.1* -t "$pkgdir"/usr/share/man/man1/
}
