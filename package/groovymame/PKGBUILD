# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: robb_force <robb_force@holybuffalo.net>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>

pkgname=(groovymame groovymame-tools groovymame-extra)
pkgver=0.247
pkgrel=2
pkgdesc="GroovyMAME is a M.A.M.E. fork aimed at CRT monitor"
_mamever=${pkgver/./}
_smamever=${pkgver#*.}
_switchresver=002l
url="https://mamedev.org/"
license=(GPL2)
arch=(x86_64)
makedepends=(nasm python asio rapidjson glm libxinerama sdl2_ttf qt5-base lua53 libutf8proc pugixml portmidi portaudio flac pulseaudio libxrandr p7zip)
source=("https://github.com/antonioginer/GroovyMAME/archive/groovymame${_mamever}.tar.gz"
        "https://github.com/antonioginer/GroovyMAME/releases/download/gm${_mamever}sr${_switchresver}/groovymame_${_mamever}.${_switchresver}_linux.tar.bz2"
         mame.sh mame.desktop mame.svg
	      "https://www.mameworld.info/mameinfo/download/Mameinfo0${_smamever}.zip"
        "https://www.arcade-history.com/dats/historyxml${_smamever}.zip"
        "https://github.com/mamedev/mame/releases/download/mame0${_mamever}/mame${_mamever}lx.zip")
)
sha256sums=(SKIP
            SKIP
            'd78e9561352cbcb814c94bad324a31efa1c6141c9e219132b0cdd3898e25f292'
            '6beb883c8efed5b7466d43d0658b47c3e4a9928b5d0245ed56446b230e28306b'
            '17c442c933d764175e4ce1de50a80c0c2ddd5d733caf09c3cd5e6ba697ac43f4'
	          SKIP
            SKIP
            SKIP)


prepare() {
  7z x "Mameinfo${_mamever}.7z"
  cd GroovyMAME-${pkgname}${_mamever}

# Use system libraries
  sed -e 's|\# USE_SYSTEM_LIB|USE_SYSTEM_LIB|g' -i makefile
}

build() {
  cd GroovyMAME-${pkgname}${_mamever}
  export CFLAGS+=" -I/usr/include/lua5.3/"
  export CXXFLAGS+=" -I/usr/include/lua5.3/"

  # Hack to force linking to lua5.3
  mkdir lib
  ln -s /usr/lib/liblua5.3.so lib/liblua.so

  export LDFLAGS+=" -L${PWD}/lib"
  make \
    NOWERROR=1 \
    OPTIMIZE=2 \
    TOOLS=1 \
    PTR64=1 NOASM=0 TARGET=mame SUBTARGET=tiny \
    ARCHOPTS=-flifetime-dse=1
}

package_groovymame() {
  depends=(sdl2_ttf qt5-base lua53 libutf8proc pugixml portmidi portaudio flac hicolor-icon-theme)
  conflicts=(sdlmame mame)
  replaces=(sdlmame mame)

  cd GroovyMAME-groovymame${_mamever}

  # Install the mame script
  install -Dm755 "$srcdir"/mame.sh "$pkgdir"/usr/bin/mame

  # Install the binaries - This one is from the GroovyMAME repo
  install -Dm755 "$srcdir"/groovymame "$pkgdir"/usr/lib/mame/${pkgname}
  # This is the binary that was compiled
  #install -Dm755 mame "$pkgdir"/usr/lib/mame/${pkgname}

  # Install the extra bits
  install -Dm644 src/osd/modules/opengl/shader/glsl*.*h -t "$pkgdir"/usr/lib/mame/shader/
  cp -ar {artwork,bgfx,plugins,language,ctrlr,keymaps,hash} "$pkgdir"/usr/lib/mame/
  install -Dm 644 uismall.bdf "$pkgdir"/usr/lib/mame/fonts/uismall.bdf
  mkdir -p "$pkgdir"/usr/share/mame
  "$pkgdir"/usr/lib/mame/groovymame -listxml > "$pkgdir"/usr/share/mame/mame.dat

  # Include the license
  install -Dm644 docs/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # FS#28203
  sed -i 's|KEYCODE_2_PAD|KEYCODE_2PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_4_PAD|KEYCODE_4PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_6_PAD|KEYCODE_6PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg
  sed -i 's|KEYCODE_8_PAD|KEYCODE_8PAD|' "$pkgdir"/usr/lib/mame/ctrlr/*.cfg

  # documentation
  install -dm0755 "$pkgdir"/usr/share/doc
  cp -a docs "$pkgdir"/usr/share/doc/$pkgname
  rm -r "$pkgdir"/usr/share/doc/$pkgname/man
  install -Dm644 docs/man/*.6* -t "$pkgdir"/usr/share/man/man6/

  # install desktop file and icon
  install -Dm644 "$srcdir"/mame.desktop -t "$pkgdir"/usr/share/applications
  install -Dm644 "$srcdir"/mame.svg -t "$pkgdir"/usr/share/icons/hicolor/scalable/apps
}

package_groovymame-tools() {
  pkgdesc='GroovyMAME is a M.A.M.E. fork aimed at CRT monitor (tools)'
  depends=(sdl2 libutf8proc flac)

  cd GroovyMAME-groovymame${_mamever}
  for _i in castool chdman floptool imgtool jedutil ldresample ldverify nltool nlwav pngcmp regrep romcmp \
            split srcclean testkeys unidasm; do
    install -Dm755 $_i -t "$pkgdir"/usr/bin
  done
  mv "$pkgdir"/usr/bin/{,mame-}split # Fix conflicts

  install -Dm644 docs/man/*.1* -t "$pkgdir"/usr/share/man/man1/
}

package_groovymame-extra() {
  pkgdesc="GroovyMame extra files"
  install -Dm755 history.xml "${pkgdir}"/usr/share/mame/dat/history.dat
  install -Dm755 mameinfo.dat "${pkgdir}"/usr/share/mame/dat/mameinfo.dat
  install -Dm755 mame${_mamever}.xml "${pkgdir}"/usr/share/mame/dat/mame.dat
}

