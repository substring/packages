pkgname=switchres
pkgver=2.2
pkgrel=2
_commit=103e83c73c178d7d9b3f94dd37194692d7891ab6
pkgdesc="Modeline generator engine and MAME resolution switcher"
url="http://forum.arcadecontrols.com/index.php/topic,106405.0.html"
license=(GPL3)
arch=(x86_64)
source=("git+https://github.com/antonioginer/switchres.git#commit=${_commit}" "rebuild_edids.sh")
sha256sums=(SKIP SKIP)
depends=('libxrandr' 'libdrm' 'sdl2' 'sdl2_ttf' 'gatools')
provides=('switchres')
conflicts=('switchres-git')
backup=('etc/switchres.ini')
install=switchres.install

build() {
  cd "${pkgname}"
  make PREFIX=/usr all grid

  sed -i 's/\r$//g' switchres.ini
  sed -i '1s+^+#!/usr/bin/env python\n+' geometry.py
}

package () {
  cd "$srcdir/${pkgname}"
  make PREFIX="$pkgdir"/usr install

  sed -i "s+$pkgdir++g" "$pkgdir"/usr/lib/pkgconfig/switchres.pc

  install -d "$pkgdir"/etc
  install -m 644 switchres.ini "$pkgdir"/etc
  install -Dm 755 switchres "$pkgdir"/usr/bin/switchres
  install -Dm 755 grid "$pkgdir"/usr/bin/grid
  install -Dm 755 geometry.py "$pkgdir"/usr/bin/geometry
  install -vDm 755 ../rebuild_edids.sh "$pkgdir"/usr/bin/rebuild_edids
}
