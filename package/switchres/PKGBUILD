pkgname=switchres
pkgver=1.53.1
pkgrel=2
pkgdesc="Modeline generator engine and MAME resolution switcher"
url="http://forum.arcadecontrols.com/index.php/topic,106405.0.html"
license=(GPL3)
arch=(x86_64)
depends=(libxml2)
source=("https://gitlab.com/groovyarcade/tools/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('722aef5cde1e8d7d0f5a2bf36df126eb1e74170780ebac87aaf88bda2ee0890d')

prepare() {
  cd ${pkgname}-${pkgver}
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd ${pkgname}-${pkgver}
  make

  cd "$srcdir/$pkgname-$pkgver"
  make

  mkdir edid && cd edid
  # Generate all EDIDs
  # 15kHz
  for m in generic_15 arcade_15 arcade_15ex k7000 k7131 h9110 polo ; do 
    ../switchres 640 480 60 --edid --monitor "$m"
  done

  # 25kHz
  for m in arcade_25 arcade_15_25 ; do 
    ../switchres 512 384 60 --edid --monitor "$m"
  done

  # 31kHz
  for m in arcade_31 arcade_15_31 arcade_15_25_31 m2929 d9200 d9400 d9800 m3129 pstar ms2930 r666b pc_31_120 vesa_480 ; do 
    ../switchres 640 480 60 --edid --monitor "$m"
  done

  # onliners, or special cases
    ../switchres 768 576 50 --edid --monitor pal
    ../switchres 720 480 60 --edid --monitor ntsc
    ../switchres 800 600 60 --edid --monitor vesa_600
    ../switchres 1024 768 60 --edid --monitor pc_70_120
    ../switchres 1024 768 60 --edid --monitor vesa_768
    ../switchres 1280 1024 60 --edid --monitor vesa_1024
}

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX="$pkgdir/usr/local" install

  install -d "$pkgdir"/usr/lib/firmware/edid
  install -m 644 edid/*.bin "$pkgdir"/usr/lib/firmware/edid
}
