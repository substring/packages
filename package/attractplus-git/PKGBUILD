_gitname=attractplus
_gitbranch=master
pkgname=${_gitname}-git
pkgver=r1076.0ed0f6d
pkgrel=2
pkgdesc="A graphical front-end for command line emulators that hides the underlying operating system and is intended to be controlled with a joystick or gamepad."
arch=('any')
url="http://www.attractmode.org/"
license=('GPL3')
depends=('openal' 'ffmpeg' 'fontconfig' 'libarchive' 'curl' 'libva-mesa-driver' 'glu' 'libsndfile' 'libxrandr' 'libxcursor' 'libjpeg' 'glew' 'freetype2' 'xcb-util-image' 'rtkit')
makedepends=('git' 'cmake' 'mesa' 'boost')
conflicts=('attract-plus')
source=("${_gitname}::git+https://github.com/oomek/${_gitname}.git#branch=${_gitbranch}"
        "PowerOptions.nut::https://raw.githubusercontent.com/oomek/attract-extra/main/plugins/PowerOptions/plugin.nut")
sha1sums=('SKIP'
          'SKIP')

pkgver() {
	cd "${_gitname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${_gitname}"
	build_x11
	build_kms
}

package() {
	cd "${_gitname}"

	cat <<'EOF' >> Makefile

install_assets: $(DATA_PATH)
	mkdir -p $(DESTDIR)$(DATA_PATH)
	cp -r config/* $(DESTDIR)$(DATA_PATH)
EOF
	make prefix="${pkgdir}/usr/local" install_assets
	install -Dm644 License.txt ${pkgdir}/usr/share/licenses/${_gitname}/License.txt
	install -vDm755 ../attractplus.sh "${pkgdir}"/usr/local/bin/attractplus
	install -vDm755 attractplus-x11 "${pkgdir}"/usr/local/bin/attractplus-x11
	install -vDm755 attractplus-kms "${pkgdir}"/usr/local/bin/attractplus-kms
	install -vDm644 ../PowerOptions.nut "${pkgdir}"/usr/local/share/attract/plugins/PowerOptions.nut
}

build_x11() {
	make clean
	make prefix=/usr/local STATIC=1 FE_HWACCEL_VAAPI=1
	mv attractplus attractplus-x11
}

build_kms() {
	make clean
	make prefix=/usr/local STATIC=1 FE_HWACCEL_VAAPI=1 USE_DRM=1
	mv attractplus attractplus-kms
}
