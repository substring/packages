_pkgname=pegasus-frontend
pkgname=${_pkgname}-git
pkgver=alpha15.r101.g8425a4f6
pkgrel=2
pkgdesc="A cross platform, customizable graphical frontend for launching emulators and managing your game collection."
arch=('i686' 'x86_64')
url="https://pegasus-frontend.org/"
license=('GPL3')
makedepends=(
    'git'
)
depends=(
    'qt5-declarative'
    'qt5-gamepad'
    'qt5-graphicaleffects'
    'qt5-imageformats'
    'qt5-multimedia'
    'qt5-quickcontrols'
    'qt5-svg'
    'qt5-tools'
    'sdl2'
    'gst-libav'
    'gst-plugins-good'
)
optdepends=(
    'polkit'
)
provides=('pegasus-frontend')
conflicts=('pegasus-frontend')
source=(
    "${_pkgname}::git+https://github.com/mmatyas/pegasus-frontend.git"
    "git+https://github.com/mmatyas/pegasus-frontend-translations.git"
    "git+https://github.com/mmatyas/pegasus-theme-grid.git"
    "git+https://github.com/mmatyas/SortFilterProxyModel.git"
)
md5sums=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
)

pkgver() {
    cd "${_pkgname}"
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${_pkgname}"
    git submodule init
    git config submodule.lang.url "${srcdir}/pegasus-frontend-translations"
    git config submodule.src/themes/pegasus-theme-grid.url "${srcdir}/pegasus-theme-grid"
    git config submodule.thirdparty/SortFilterProxyModel.url "${srcdir}/SortFilterProxyModel"
    git submodule update
    patch -Np1 < etc/rpi4/kms_launch_fix.diff
}

build() {
    cd "${srcdir}/${_pkgname}"
    mkdir -p ./build && cd build
    export QMAKE_LIBS_LIBDL=-ldl
    export QMAKE_CFLAGS_RELEASE="$(CFLAGS)"
    export QMAKE_CXXFLAGS_RELEASE="$(CXXFLAGS)"

    qmake .. \
        USE_SDL_GAMEPAD=1 \
        USE_SDL_POWER=1 \
        INSTALL_BINDIR=/usr/bin \
        INSTALL_DOCDIR=/usr/share/doc/pegasus-frontend \
        INSTALL_ICONDIR=/usr/share/pixmaps \
        INSTALL_DESKTOPDIR=/usr/share/applications \
        INSTALL_APPSTREAMDIR=/usr/share/metainfo \
        QMAKE_LIBS_LIBDL="-ldl" \
        QMAKE_CFLAGS_RELEASE="$(CFLAGS)" \
        QMAKE_CXXFLAGS_RELEASE="$(CXXFLAGS)"
    make
}

package() {
    cd "${srcdir}/${_pkgname}/build"
    make INSTALL_ROOT="${pkgdir}/" install
}
