pkgname=switchres-git
_gitname=${pkgname%-git}
pkgver=r405.5066e92
pkgrel=2
pkgdesc="Modeline generator engine and MAME resolution switcher"
url="http://forum.arcadecontrols.com/index.php/topic,106405.0.html"
license=(GPL3)
arch=(x86_64)
source=("switchres::git+https://github.com/antonioginer/switchres.git" "rebuild_edids.sh")
sha256sums=(SKIP SKIP)
depends=('libxrandr' 'libdrm' 'sdl2' 'sdl2_ttf' 'gatools')
provides=('switchres')
conflicts=('switchres')
backup=('etc/switchres.ini')
install=switchres.install

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_gitname}"
  make PREFIX=/usr all grid

  sed -i 's/^M$//g' switchres.ini
  sed -i '1s+^+#!/usr/bin/env python\n+' geometry.py
}

package () {
  cd "$srcdir/${_gitname}"
  make PREFIX="$pkgdir"/usr install

  sed -i "s+$pkgdir++g" "$pkgdir"/usr/lib/pkgconfig/switchres.pc

  install -d "$pkgdir"/etc
  install -m 644 switchres.ini "$pkgdir"/etc
  install -Dm 755 switchres "$pkgdir"/usr/bin/switchres
  install -Dm 755 grid "$pkgdir"/usr/bin/grid
  install -Dm 755 geometry.py "$pkgdir"/usr/bin/geometry
  install -vDm 755 ../rebuild_edids.sh "$pkgdir"/usr/bin/rebuild_edids
}
