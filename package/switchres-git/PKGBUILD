pkgname=switchres-git
_gitname=${pkgname%-git}
pkgver=r405.5066e92
pkgrel=2
pkgdesc="Modeline generator engine and MAME resolution switcher"
url="http://forum.arcadecontrols.com/index.php/topic,106405.0.html"
license=(GPL3)
arch=(x86_64)
source=("switchres::git+https://github.com/antonioginer/switchres.git")
sha256sums=(SKIP)
depends=('libxrandr' 'libdrm' 'sdl2')
provides=('switchres')
conflicts=('switchres')

pkgver() {
  cd "${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${_gitname}"
  make PREFIX=/usr all grid

  sed -i 's/^M$//g' switchres.ini
  mkdir edid && cd edid
  # Generate all EDIDs
  # 15kHz
  for m in generic_15 arcade_15 arcade_15ex k7000 k7131 h9110 polo ; do
    ../switchres 640 480 60 --edid --monitor "$m"
  done

  # 25kHz
  for m in arcade_25 arcade_15_25 ; do
    ../switchres 512 384 60 --edid --monitor "$m"
  done

  # 31kHz
  for m in arcade_31 arcade_15_31 arcade_15_25_31 m2929 d9200 d9400 d9800 m3129 pstar ms2930 ms929 r666b pc_31_120 vesa_480 ; do
    ../switchres 640 480 60 --edid --monitor "$m"
  done

  # onliners, or special cases
  ../switchres 768 576 50 --edid --monitor pal
  ../switchres 720 480 60 --edid --monitor ntsc
  ../switchres 800 600 60 --edid --monitor vesa_600
  ../switchres 1024 768 60 --edid --monitor pc_70_120
  ../switchres 1024 768 60 --edid --monitor vesa_768
  ../switchres 1280 1024 60 --edid --monitor vesa_1024

  # Now generate a super res 15kHz edid wihth crt_range from generic_15
  echo "monitor custom" > ./switchres.ini
  echo "dotclock_min 25.0" > ./switchres.ini
  echo "crt_range0  15625-15750, 49.50-65.00, 2.000, 4.700, 8.000, 0.064, 0.192, 1.024, 0, 0, 192, 288, 448, 576" > ./switchres.ini
  cat switchres.ini
  ../switchres 640 480 60 --edid --monitor custom
  mv custom.bin generic_15_super_res.bin
}

package () {
  cd "$srcdir/${_gitname}"
  make PREFIX="$pkgdir"/usr install

  sed -i "s+$pkgdir++g" "$pkgdir"/usr/lib/pkgconfig/switchres.pc

  install -d "$pkgdir"/usr/lib/firmware/edid
  install -d "$pkgdir"/etc
  install -m 644 edid/*.bin "$pkgdir"/usr/lib/firmware/edid
  install -m 644 switchres.ini "$pkgdir"/etc
  install -Dm 755 switchres "$pkgdir"/usr/bin/switchres
  install -Dm 755 grid "$pkgdir"/usr/bin/grid
}
