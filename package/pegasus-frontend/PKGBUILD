pkgname=pegasus-frontend
pkgver=0.15
_pkgver=alpha15
pkgrel=1
pkgdesc="A cross platform, customizable graphical frontend for launching emulators and managing your game collection."
arch=('i686' 'x86_64')
url="https://pegasus-frontend.org/"
license=('GPL3')
makedepends=(
    'git'
)
depends=(
    'qt5-declarative'
    'qt5-gamepad'
    'qt5-graphicaleffects'
    'qt5-imageformats'
    'qt5-multimedia'
    'qt5-quickcontrols'
    'qt5-svg'
    'qt5-tools'
    'sdl2'
    'gst-libav'
    'gst-plugins-good'
)
optdepends=(
    'polkit'
)
provides=('pegasus-frontend')
conflicts=('pegasus-frontend-git')
source=(
    "${pkgname}::git+https://github.com/mmatyas/pegasus-frontend.git#tag=${_pkgver}"
)
md5sums=(
    'SKIP'
)

prepare() {
    cd "${srcdir}/${pkgname}"
    git submodule init
    git submodule update
    patch -Np1 < etc/rpi4/kms_launch_fix.diff
}

build() {
    cd "${srcdir}/${pkgname}"
    mkdir -p ./build && cd build
    export QMAKE_LIBS_LIBDL=-ldl USE_SDL_GAMEPAD=1 USE_SDL_POWER=1

    qmake .. \
        QMAKE_LIBS_LIBDL=-ldl \
        USE_SDL_GAMEPAD=1 \
        USE_SDL_POWER=1 \
        INSTALL_BINDIR=/usr/bin \
        INSTALL_DOCDIR=/usr/share/doc/pegasus-frontend \
        INSTALL_ICONDIR=/usr/share/pixmaps \
        INSTALL_DESKTOPDIR=/usr/share/applications \
        INSTALL_APPSTREAMDIR=/usr/share/metainfo
    make
}

package() {
    cd "${srcdir}/${pkgname}/build"
    make INSTALL_ROOT="${pkgdir}/" install
}

