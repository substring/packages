pkgname=gasetup-git
_gitname=${pkgname%-git}
pkgver=r19.d973d65
pkgrel=1
pkgdesc="GroovyArcade setup scripts"
arch=(any)
license=(GPL-2)
depends=(bash unzip wget curl p7zip dialog squashfs-tools which binutils gatools-git galauncher-git)
makedepends=(git)
provides=('gasetup')
conflicts=('gasetup')
source=("git+https://gitlab.com/groovyarcade/gasetup.git")
sha256sums=(SKIP)

pkgver() {
  cd "$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir/$_gitname"
  sed -i "/\/usr\/share/d" Makefile
  make DESTDIR="${pkgdir}"/opt/gasetup install

  install -dm 755 "${pkgdir}"/usr/share/{applications,pixmaps}
  install -m 644 gasetup.desktop "${pkgdir}"/usr/share/applications
  install -m 644 gasetup.png "${pkgdir}"/usr/share/pixmaps
  install -Dm 644 groovyarcade.hook "${pkgdir}"/usr/share/libalpm/hooks/groovyarcade.hook
}
