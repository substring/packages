pkgname=fbneo
pkgver=1.0.0.2
pkgrel=2
pkgdesc="Final Burn Neo is a arcade and console emulator with switchres support"
arch=('x86_64')
license=('custom')
depends=('sdl2' 'sdl2_image' 'switchres')
source=("git+https://github.com/finalburnneo/FBNeo.git#tag=v${pkgver}"
        '0001-fx_sr_2_2.patch'
        '0002-SDL2-change-F12-key-to-ESCAPE.patch')
sha256sums=('SKIP'
            '8566c86db15519c8724a432a3d0fcda619b18a7b06ee7fdd0c27b973084dc2be'
            'd11552392eea76315bb0dcf471327691aec878687e3535d10091d038583fb858')
_srcname=FBNeo


prepare() {
  cd "${srcdir}"/$_srcname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}


build() {
  cd "${srcdir}"/$_srcname

  make sdl2 INCLUDE_SWITCHRES=1 RELEASEBUILD=1
}


package() {
  cd "${srcdir}"/$_srcname

  install -Dm755 fbneo "$pkgdir"/usr/local/bin/fbneo
  install -Dm644 src/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
